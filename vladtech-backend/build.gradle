plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'org.example'
version = '0.0.1-SNAPSHOT'
description = 'VladTech'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

ext {
	mapstructVersion = "1.6.3"
	lombokVersion = "1.18.36"
	lombokMapstructBindingVersion = "0.2.0"
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {

	/* ---------------- CORE SPRING ---------------- */
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-mail'

	/* ---------------- SECURITY ---------------- */
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	/* ---------------- DATABASE ---------------- */
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

	/* ---------------- UTIL ---------------- */
	implementation 'org.apache.commons:commons-lang3:3.18.0'

	/* ---------------- LOMBOK ---------------- */
	compileOnly "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

	/* ---------------- MAPSTRUCT ---------------- */
	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
	annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

	/* ---------------- DEV ---------------- */
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	/* ---------------- TEST ---------------- */
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation "de.flapdoodle.embed:de.flapdoodle.embed.mongo.spring3x:4.12.3"
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
	testLogging {
		events "passed", "skipped", "failed"
	}
	systemProperty 'spring.profiles.active',
			System.getProperty('spring.profiles.active', 'test')
}

jacoco {
	toolVersion = "0.8.12"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					'**/config/**',
					'**/entity/**',
					'**/*Application.class',
					'**/utils/DatabaseLoaderService.class'
			])
		}))
	}
}
